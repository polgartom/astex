open_buffer_browser :: () {
    using buffer_browser;

    if bb_text_input {
        select_all(bb_text_input);
    } else {
        bb_text_input = create_text_input(BUFFER_BROWSER_UI_ID);
    }
    
    array_reset_keeping_memory(*options);
    array_reserve(*options, buffers.count);

    wd := get_workdir();

    for b: buffers {
        if !b.initialized continue;

        title := "* New *";
        if b.exists_on_disk {
            assert(!!b.filepath);

            title = b.filepath;
            if begins_with(title, wd) {
                advance(*title, wd.count);
            }
        } else if b.name {
            title = b.name;
        }

        opt := array_add2(*options);
        opt.ref = b;
        opt.priority = 0;
        opt.label_styled.default_colorv4 = map_color_to_vec4(.TEXT_DEFAULT);
        modify_string(*opt.label, title);
        
        if !b.readonly opt.priority = 1; // prioritize the non-scratch buffers
    }

    update_visible_options(*buffer_browser);

    switch_editor_mode(.WIDGET);
    active_widget = Buffer_Browser;
}

update_visible_options :: (using browser: *Buffer_Browser) {
    assert(bb_list_input_ui_id != 0 && bb_text_input != null);
    
    list_input := find_or_create_list_input(bb_list_input_ui_id);
    list_input.selected_index = 0;
    list_input.scroll_value = 0.0;
    
    tinp := bb_text_input;
    
    array_reset_keeping_memory(*visible_options);
    array_reserve(*visible_options, options.count);

    for opt: options {
        if tinp.text {
            found, rem := contains_nocase(opt.label, tinp.text);
            if !found continue;
        }

        // log("opt: %\n", opt);
        array_add(*visible_options, opt);
    }
    
    quick_sort(visible_options, (a, b) => cast(s64) ((b.priority) - (a.priority)));
}

BUFFER_BROWSER_UI_ID :: cast(Ui_Id) #location();

buffer_browser: Buffer_Browser;

Buffer_Browser :: struct {
    search_text: string;

    options: [..] List_Input.Option(*Buffer);
    visible_options: [..] List_Input.Option(*Buffer);

    bb_text_input: *Text_Input;
    bb_list_input_ui_id := BUFFER_BROWSER_UI_ID;
}